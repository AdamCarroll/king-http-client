// Copyright (C) king.com Ltd 2015
// https://github.com/king/king-http-client
// Author: Magnus Gustafsson
// License: Apache 2.0, https://raw.github.com/king/king-http-client/LICENSE-APACHE

plugins {
	id 'java'
	id 'java-library'
	id 'net.ltgt.errorprone'
	id 'jacoco'
	id 'maven-publish'
	id 'signing'
}

group 'com.king.king-http-client'
version '3.0.25'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
ext.admin = hasProperty('sonatypeUsername')

archivesBaseName = 'king-http-client'

dependencies {
	api "io.netty:netty-all:$nettyVersion"
	testImplementation 'ch.qos.logback:logback-classic:1.3.0-alpha5'
}

publishing {
	repositories {
		mavenLocal()
		maven {
			url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
			credentials {
				username sonatypeUsername
				password sonatypePassword
			}
		}
	}
	publications {
		mavenJava(MavenPublication) {

			artifactId = 'king-http-client'

			from components.java

			pom {
				name = 'King Http Client'
				description = 'A asyncronous http client built ontop of netty.'

				url = 'https://github.com/king/king-http-client'

				scm {
					connection =  'scm:git:https://github.com/king/king-http-client.git'
					developerConnection = 'scm:svn:https://github.com/king/king-http-client.git'
					url = 'https://github.com/king/king-http-client'
				}

				licenses {
					license {
						name = 'The Apache License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}

				developers {
					developer {
						id = 'magnus-gustafsson'
						name = 'Magnus Gustafsson'
						email = 'magnus.gustafsson@king.com'
					}
				}

			}
		}
	}
}

java {
	withJavadocJar()
	withSourcesJar()
}

javadoc {
	if(JavaVersion.current().isJava9Compatible()) {
		options.addBooleanOption('html5', true)
	}
}

signing {
	required { admin }
	sign publishing.publications.mavenJava
}
